apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion(COMPILE_SDK_VERSION.toInteger())
    buildToolsVersion(BUILD_TOOLS_VERSION)

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {

        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {

        buildTypes.release.signingConfig = null
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'UnusedResources' // Lint still does not detect usage of resources from Kotlin code.
    }
}

def ankoVersion = "0.9"

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(":data")
    compile project(":component")

    // Android support libraries
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBS_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBS_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBS_VERSION"

    // Reactive library
    compile "io.reactivex:rxkotlin:0.60.0"
    compile "io.reactivex:rxandroid:1.2.1"

    // DSL
    compile "org.jetbrains.anko:anko-sdk15:$ankoVersion" // sdk19, sdk21, sdk23 are also available
    compile "org.jetbrains.anko:anko-support-v4:$ankoVersion" // In case you need support-v4 bindings
    compile "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion" // For appcompat-v7 bindings

    // Artwork
    compile "com.squareup.picasso:picasso:2.5.2"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$KOTLIN_VERSION"
    }
}
