apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.nekocode'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 127
        versionName projectVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def supportLibVersion="23.1.1"
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android support library
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"

    // Reactive library
    compile "io.reactivex:rxkotlin:0.40.1"
    compile "io.reactivex:rxandroid:1.1.0"

    // Kotlin std library
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // DSL
    compile "org.jetbrains.anko:anko-sdk15:0.8.2" // sdk19, sdk21, sdk23 are also available
    compile "org.jetbrains.anko:anko-support-v4:0.8.2" // In case you need support.v4 bindings

    // Event Bus
    compile 'com.squareup:otto:1.3.8'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}