apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.nekocode'

android {
    compileSdkVersion vers.compileSdk
    buildToolsVersion vers.buildTools

    defaultConfig {
        minSdkVersion vers.minSdk
        targetSdkVersion vers.compileSdk
        versionCode vers.versionCode
        versionName vers.versionName
    }

    packagingOptions {
        exclude 'LICENSE.txt' // multiple libs have this file -> cause build error
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android support library
    provided "com.android.support:appcompat-v7:$vers.supportLibs"
    provided "com.android.support:recyclerview-v7:$vers.supportLibs"

    // Reactive library
    provided "io.reactivex:rxkotlin:$vers.rxKotlin"
    provided "io.reactivex:rxandroid:$vers.rxAndroid"

    // DSL
    provided "org.jetbrains.anko:anko-sdk15:$vers.anko" // sdk19, sdk21, sdk23 are also available
    provided "org.jetbrains.anko:anko-appcompat-v7:$vers.anko" // For appcompat-v7 bindings

    provided "org.jetbrains.kotlin:kotlin-stdlib:$vers.kotlin"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// Build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}